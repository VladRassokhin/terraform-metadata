plugins {
}

apply plugin: 'java'
apply plugin: 'maven-publish'

group = 'org.intellij.plugins.hcl.terraform'

def versionPrefix = '0.2'
boolean isSnapshot = true
if (System.getProperty('build.number') != null) {
    version = "${versionPrefix}.${System.getProperty('build.number')}"
    isSnapshot = false
} else {
    version = "${versionPrefix}.SNAPSHOT"
    isSnapshot = true
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

processResources {
    doLast {
        if (!isSnapshot) {
            print "${outputs.files.asPath}"
            fileTree(dir: outputs.files.asPath, include: '**/*.json').each {
                file -> file.text = groovy.json.JsonOutput.toJson(new groovy.json.JsonSlurper().parse(file))
            }
        }
    }
}
sourceSets.main.resources { srcDir 'terraform' }
jar {
    includeEmptyDirs false
    eachFile { details ->
        details.path = "terraform/$details.path"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version
            suppressAllPomMetadataWarnings()
        }
    }
    repositories {
        maven {
            url "https://packages.jetbrains.team/maven/p/ij/intellij-dependencies"
            credentials {
                username = "$space_username"
                password = "$space_password"
            }
        }
    }
}

